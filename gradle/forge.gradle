repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name "Cyclops Repo"
        url "https://cyclopsmc.jfrog.io/cyclopsmc/libs-release/"
    }
    maven {
        name "Cyclops Third Party Repo"
        url "https://dl.bintray.com/cyclopsmc/thirdparty/"
    }
    maven {
        name "Amadornes"
        url "http://maven.amadornes.com/"
    }
    maven {
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        name = "Charset Maven"
        url = "http://charset.asie.pl/maven"
    }
    maven {
        name = "Dankhax"
        url = "http://maven.epoxide.org"
    }
    maven {
        name 'Player Maven FS'
        url 'http://maven.ic2.player.to/'
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        name 'tterrag'
        url 'http://maven.tterrag.com/'
    }
    maven {
        name "Raoulvdberge Repo"
        url "https://dl.bintray.com/raoulvdberge/dev/"
    }
    maven {
        name "Blamejared Maven Repo"
        url "http://maven.blamejared.com/"
    }
    maven {
        name 'K4 maven'
        url "http://maven.k-4u.nl/"
    }
}
dependencies {
    // Add something like 'integrateddynamics_version_local=0.1.0-DEV' to your gradle.properties if you want to use a custom local Integrated Dynamics version.
    if(project.hasProperty("integrateddynamics_version_local")) {
        compile "org.cyclops.integrateddynamics:IntegratedDynamics:${config.minecraft_version}-${project.integrateddynamics_version_local}:deobf"
    } else {
        compile "org.cyclops.integrateddynamics:IntegratedDynamics:${config.minecraft_version}-${config.integrateddynamics_version}:deobf"
    }
    // Add something like 'cyclopscore_version_local=0.1.0-DEV' to your gradle.properties if you want to use a custom local CyclopsCore version.
    if(project.hasProperty("cyclopscore_version_local")) {
        compile "org.cyclops.cyclopscore:CyclopsCore:${config.minecraft_version}-${project.cyclopscore_version_local}:deobf"
    } else {
        compile "org.cyclops.cyclopscore:CyclopsCore:${config.minecraft_version}-${config.cyclopscore_version}:deobf"
    }

    deobfCompile("org.cyclops.commoncapabilities:CommonCapabilities:${config.commoncapabilities_version}") { // https://dl.bintray.com/cyclopsmc/dev/org/cyclops/commoncapabilities/CommonCapabilities/
        exclude group: 'org.cyclops.cyclopscore', module: 'CyclopsCore'
    }

    //deobfCompile "MCMultiPart:MCMultiPart-experimental:${config.mcmultipart_version}:universal" // http://maven.amadornes.com/
    deobfCompile "mcp.mobius.waila:Waila:${config.waila_version}" // http://mobiusstrip.eu/maven/mcp/mobius/waila/Waila/
    deobfCompile "mezz.jei:jei_${config.jei_version}" // https://dvs1.progwml6.com/jenkins/
    //deobfCompile "pl.asie.charset:charset-api:${config.charset_version}" // http://charset.asie.pl/maven/pl/asie/charset/charset-api/maven-metadata.xml
    deobfCompile "net.darkhax.tesla:Tesla-${config.teslaapi_version}" // http://maven.epoxide.org/net/darkhax/tesla/Tesla/
    deobfCompile "slimeknights.mantle:Mantle:${config.mantle_version}" // https://dvs1.progwml6.com/jenkins/
    deobfCompile "slimeknights:TConstruct:${config.tconstruct_version}" // http://dvs1.progwml6.com/files/maven/slimeknights/TConstruct/maven-metadata.xml
    deobfCompile "net.sengir.forestry:forestry_${config.forestry_version}" // http://maven.ic2.player.to/
    deobfCompile "net.industrial-craft:industrialcraft-2:${config.IC2_version}" // http://maven.ic2.player.to/
    deobfCompile "the-one-probe:theoneprobe:${config.top_version}" // https://minecraft.curseforge.com/api/maven/
    //deobfCompile "mcjty.theoneprobe:TheOneProbe:${config.top_version}" // http://maven.tterrag.com/mcjty/theoneprobe/TheOneProbe/
    deobfCompile "com.github.mcjty:compatlayer:${config.compatlayer_version}" // http://maven.k-4u.nl/com/github/mcjty/compatlayer/
    deobfCompile "refinedstorage:refinedstorage:${config.refinedstorage_version}" // https://dl.bintray.com/raoulvdberge/dev/refinedstorage/refinedstorage/
    deobfCompile "blusunrize:ImmersiveEngineering:${config.ie_version}"// http://maven.blamejared.com/blusunrize/ImmersiveEngineering/
    deobfCompile "CraftTweaker2:CraftTweaker2-API:${config.crafttweakerapi_version}"; // http://maven.blamejared.com/CraftTweaker2/CraftTweaker2-API/
    deobfCompile "net.cyclops.thirdparty:Signals-${config.signals_version}"; // https://dl.bintray.com/cyclopsmc/thirdparty/net/cyclops/thirdparty/signals/maven-metadata.xml

    // Only needed for testing
    //deobfCompile "pl.asie.charset:charset-lib:${config.charset_version}" // http://charset.asie.pl/maven/pl/asie/charset/charset-lib/maven-metadata.xml
    //deobfCompile "pl.asie.charset:charset-pipes:${config.charset_version}" // http://charset.asie.pl/maven/pl/asie/charset/charset-pipes/maven-metadata.xml
    deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:${config.crafttweakermain_version}" // http://maven.blamejared.com/CraftTweaker2/CraftTweaker2-MC1120-Main/

    // Project lombok
    compile "org.projectlombok:lombok:1.14.4"
}


minecraft {
    version = "${config.minecraft_version}-${config.forge_version}"
    runDir = "run"
    
    replaceIn "Reference.java"
    replace "@VERSION@", project.version
    replace "@MC_VERSION@", project.config.minecraft_version
    replace "@FORGE_VERSION@", project.config.forge_version
    replace "@BUILD_NUMBER@", project.buildnumber
    replace "@CYCLOPSCORE_VERSION@", project.config.cyclopscore_version

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "${config.mcp_mappings_version}"
}

if (project.buildnumber.equals("RELEASE"))
    version = "${config.minecraft_version}-${config.mod_version}"
else
    version = "${config.minecraft_version}-${config.mod_version}-${buildnumber}"

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, that is not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
